// figure-11.box
Simulation sim {
  .iterations = 30
  .silent = TRUE
  Box random {
    RandomiserStratified randomiser {
    }
    RandomUniformInt k {
      .min = 15
      .max = 30
    }
    RandomNormal cropAtStart {
      .min = 10
      .max = 30 
    }
    RandomNormal cropHalfways {
      .min = 750 
      .max = 850 
    }
    RandomNormal propExposedImmigrants {
      .min = 0.1
      .max = 0.7
    }
    RandomNormal lethalTime {
      .min = 50
      .max = 115
    }
    RandomNormal immunityCost {
      .min = 0
      .max = 0.4
    }
    RandomNormal sporulationOn {
      .min = 80 
      .max = 99 
    }
    RandomNormal cadaverDuration {
      .min = 48 
      .max = 112 
    }
    RandomNormal timeAcceleration {
      .min = 1
      .max = 3
    }
    RandomNormal transmissionEfficiency {
      .min = 0.1
      .max = 4.0 // 0.5
    }
  }
  Box param {
    &juvenileApterousDuration = 172
    &juvenileAlateDuration = 195 
    &adultDuration = 300
    &fecundityDuration = 100
    &fecundity_k = 1
  }
  Calendar calendar {
    .begin = 01/04/1995
    .end   = 31/08/1995
  }
  Records weather {
    .fileName = "validation/K95-weather-gs.txt"
  } 
  Box wheat{
    CropGrowthStage cropGrowthStageModel {
      .valueAtStart       = 20.0
      .dayDegreesHalfways = 800.0
      .slope              = 2.8
      .dayDegrees = ./time[total]
      CropIsGrowing isGrowing {
        .temperature = weather[Tavg] 
      }
      DayDegrees time{
        .T = weather[Tavg]
        .T0 = 0
        .isTicking = ../isGrowing[value]
      }
    }
    Box cropGrowthStage {
      &value = if exists(weather[GrowthStage]) then weather[GrowthStage] else ../cropGrowthStageModel[value]
    }
  }
  Maker system {
    // The system contains two aphid populations, 
    // identical except for the proportion of exposed immigrants
    .names = c("withoutFungus", "withFungus")
    Box { // withoutFungus or withFungus
      DayDegrees time{
        .T = weather[Tavg]
        .T0 = 3 
        .Topt = 18 
        .Tmax = 30 
      }  
      DayDegrees fungusTime {
        .T = weather[Tavg]
        .T0 = 2 
        .Topt = 18 
        .Tmax = 30 
      }
      Box aphidDensity {
        &nymphs      = sum(../*/nymph/*[content])
        &adults      = sum(../*/adult/*[content])
        &total       = .[nymphs] + .[adults]
        &susceptible = sum(../susceptible/*/*[content])
        &exposed     = sum(../exposed/*/*[content])
        &cadavers    = sum(../infectious/cadavers[content])
      }
      AphidImmigration immigration {
        .cropGrowthStage = cropGrowthStage[value]
        .toCropGrowthStage = 69
        .immigrationRate = 0.00015 // Calibrated rate; default is 0.02 (for outbreak)
        .propExposedImmigrants = if name(..[]) == "withFungus"
                                 then random/propExposedImmigrants[value]
                                 else 0.0
        .k = random/k[value]
      }
      AphidNetReproduction netReproduction {
        .Tmin = 3 
        .Topt = 16.1 
        .Tmax = 30
        .R0opt = 51.6
        .temperature = weather[Tavg]
        .cropGrowthStage = cropGrowthStage[value]
        .optimumCropGrowthStageMin = 59
        .optimumCropGrowthStageMax   = 73
        .optimumCropFactor = 1.6
        .aphidDensity = ../aphidDensity[total]
        .aphidDensityThreshold = 40 
        .alateFactor = 0.67
        .immunityCost = random/immunityCost[value]
      }
      AphidOffspring offspring {
        .offspringTotal = sum(../*/adult/*/fecundity[outflow]) 
        .aphidDensity = ../aphidDensity[total]
        .cropGrowthStage = cropGrowthStage[value]
      }
      AphidJuvenileSurvival survival{
        .cropGrowthStage = cropGrowthStage[value]
        .temperature = weather[Tavg]
      }
      Box susceptible {
        Box nymph {
          Stage apterous {
            .inflow       = ancestors::*/offspring[apterous]
            .timeStep     = ancestors::*/time[step]
            .growthFactor = ancestors::*/survival[value]
            .k = random/k[value]
            .duration = param[juvenileApterousDuration]
            .phaseOutflowProportion = ancestors::*/infectious/infectionRate[value]
          }
          Stage alate {
            .inflow = ancestors::*/offspring[alate]
            .timeStep = ancestors::*/time[step]
            .k = random/k[value]
            .duration = param[juvenileAlateDuration]
            .growthFactor = ancestors::*/survival[value]
            .phaseOutflowProportion = ancestors::*/infectious/infectionRate[value]
          }
        }
        Box adult {
          Stage apterous {
            .inflow = ancestors::*/nymph/apterous[outflow]
            .timeStep = ancestors::*/time[step]
            .duration = param[adultDuration]
            .k = random/k[value]
            .phaseOutflowProportion = ancestors::*/infectious/infectionRate[value]
            Stage fecundity {
              .inflow = ancestors::*/nymph/apterous[outflow]
              .timeStep = ancestors::*/time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = ancestors::*/netReproduction[apterous]
              .phaseOutflowProportion = ancestors::*/infectious/infectionRate[value]
            }
          }
          Stage alate {
            .inflow = ancestors::*/immigration[susceptible]
            .timeStep = ancestors::*/time[step]
            .duration = param[adultDuration]
            .k = random/k[value]
            .phaseOutflowProportion = ancestors::*/infectious/infectionRate[value]
            Stage fecundity {
              .inflow = ancestors::*/immigration[susceptible]
              .timeStep = ancestors::*/time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = ancestors::*/netReproduction[alate]
              .phaseOutflowProportion = ancestors::*/infectious/infectionRate[value]
            }
          }
        }
      } // susceptible
      Box exposed {
        Box nymph {
          StageAndPhase apterous {
            .timeStep = ancestors::*/time[step]
            .k = random/k[value]
            .duration = param[juvenileApterousDuration]
            .growthFactor = ancestors::*/survival[value]
            .phaseInflow = ancestors::*/susceptible/nymph/apterous[phaseOutflow]
            .phaseTimeStep = ancestors::*/fungusTime[step]
            .phaseK = random/k[value]
            .phaseDuration = random/lethalTime[value]
          }
          StageAndPhase alate {
            .timeStep = ancestors::*/time[step]
            .k = random/k[value]
            .duration = param[juvenileAlateDuration]
            .growthFactor = ancestors::*/survival[value]
            .phaseInflow = ancestors::*/susceptible/nymph/alate[phaseOutflow]
            .phaseTimeStep = ancestors::*/fungusTime[step]
            .phaseK = random/k[value]
            .phaseDuration = random/lethalTime[value]
          }
        } // nymph
        Box adult {
          StageAndPhase apterous {
            .inflow = ancestors::*/nymph/apterous[outflow]
            .timeStep = ancestors::*/time[step]
            .duration = param[adultDuration]
            .k = random/k[value]
            .phaseInflow = ancestors::*/susceptible/adult/apterous[phaseOutflow]
            .phaseTimeStep = ancestors::*/fungusTime[step]
            .phaseK = random/k[value]
            .phaseDuration = random/lethalTime[value]
            StageAndPhase fecundity {
               // No inflow because exposed/nymphs don't reproduce as adults
              .timeStep = ancestors::*/time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = ancestors::*/netReproduction[apterousExposed] 
              .phaseInflow = ancestors::*/susceptible/adult/apterous/fecundity[phaseOutflow]
              .phaseTimeStep = ancestors::*/fungusTime[step]
              .phaseK = random/k[value]
              .phaseDuration = random/lethalTime[value]
            }
          }
          StageAndPhase alate {
            .inflow = ancestors::*/immigration[exposed]
            .timeStep = ancestors::*/time[step]
            .duration = param[adultDuration]
            .k = random/k[value]
            .phaseInflow = ancestors::*/susceptible/adult/alate[phaseOutflow]
            .phaseTimeStep = ancestors::*/fungusTime[step]
            .phaseK = random/k[value]
            .phaseDuration = random/lethalTime[value]
            StageAndPhase fecundity {
              // Exposed immigrants reproduce after arriving
              .inflow = ancestors::*/immigration[exposed]
              .timeStep = ancestors::*/time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = ancestors::*/netReproduction[alateExposed] 
              .phaseInflow = ancestors::*/susceptible/adult/alate/fecundity[phaseOutflow]
              .phaseTimeStep = ancestors::*/fungusTime[step]
              .phaseK = random/k[value]
              .phaseDuration = random/lethalTime[value]
            }
          }
        } // adult
        CadaverConversion succumbed {
          .succumbedApterousNymphs = sum(ancestors::*/nymph/apterous[phaseOutflow])
          .succumbedAlateNymphs    = sum(ancestors::*/nymph/alate[phaseOutflow])
          .succumbedApterousAdults = sum(ancestors::*/adult/apterous[phaseOutflow])
          .succumbedAlateAdults    = sum(ancestors::*/adult/alate[phaseOutflow])
        }
      } // exposed
      Box infectious {
        OnOff isSporulating {
          .x = weather[RHmax]
          .xOn = random/sporulationOn[value]
          .xOff = 999 
        }
        CadaverTime time {
          .isSporulating = ../isSporulating[isOn]
          .timeStep = ancestors::*/fungusTime[step]
          .rhAccelaration = random/timeAcceleration[value]
        }
        Stage cadavers {
          .inflow = ancestors::*/exposed/succumbed[cadavers]
          .timeStep = ../time[step]
          .duration = random/cadaverDuration[value]
          .k = random/k[value]
        }
        InfectionRate infectionRate {
          .isSporulating = ../isSporulating[isOn]
          .cadavers = ../cadavers[content]
          .transmissionEfficiency = random/transmissionEfficiency[value]
        }
      } // infectious
      Box diagnostics {
        Accumulator aphidDays {
          .change = ancestors::*/aphidDensity[total]
        }
        Accumulator cadaverDays {
          .change = ../../infectious/cadavers[content]
        }
        Prevalence prevalence {
          .aphidDensity   = ancestors::*/aphidDensity[total]
          .exposedDensity = ancestors::*/aphidDensity[exposed]
          .cadaverDensity = ancestors::*/aphidDensity[cadavers]
        }
        AphidIndex aphidIndex {
          .nymphs = ancestors::*/aphidDensity[nymphs]
          .adults = ancestors::*/aphidDensity[adults]
        }
        aphid::Yield yield {
          .aphidIndex = ../aphidIndex[value]
          .cropGrowthStage = cropGrowthStage[value]
        }
      } // diagnostics
    } // withoutFungus or withFungus
  } // system
  Biocontrol biocontrol {
    .aphidPressureWithoutF = withoutFungus/diagnostics/aphidDays[value] 
    .aphidPressureWithF = withFungus/diagnostics/aphidDays[value] 

    .yieldWithoutF = withoutFungus/diagnostics/yield[yield] 
    .yieldWithF = withFungus/diagnostics/yield[yield] 
    
    .cropGrowthStage = cropGrowthStage[value]
    .prevalence = withFungus/diagnostics/prevalence[exposed]
    .cadaverPrevalence = withFungus/diagnostics/prevalence[cadavers]
  }
  OutputR {
    .code = "subtitle='Aphid immigration rate = " + withFungus/immigration[immigrationRate] + " per tiller per day'"
    .scripts = "figure-11.R"
    Box totals {
      &nymphSusc = sum(withFungus/susceptible/nymph/*[content])
      &nymphExp  = sum(withFungus/exposed/nymph/*[content])
      &adultSusc = withFungus/susceptible/adult/apterous[content]
      &adultExp  = withFungus/exposed/adult/apterous[content]
      &cadavers  = withFungus/exposed/succumbed[cadavers] + withFungus/infectious/cadavers[content]
    }
    OutputText {
      .ports =  calendar[date] | ../totals[*] | cropGrowthStage[value]
    }
  }  
}